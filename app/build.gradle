apply plugin: 'com.android.application'
apply plugin: "org.sonarqube"

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        versionCode 247
        versionName "38.01"

        applicationId "org.andstatus.app"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        testApplicationId "org.andstatus.app.tests"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // To test arguments:
        // testInstrumentationRunnerArgument "executionMode", "travisTest"
        project.ext.archivesBaseName = "AndStatus-$versionName".toString()
        project.ext.versionName = "$versionName".toString()
    }

    buildTypes {
        release {
            minifyEnabled false
            lintOptions {
                warning 'MissingTranslation','InvalidPackage'
            }
        }
        debug {
            testCoverageEnabled = project.hasProperty('testCoverageEnabled') ? project.testCoverageEnabled as boolean : false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
}

sonarqube {
    // See https://docs.sonarqube.org/display/SCAN/Analyzing+with+SonarQube+Scanner+for+Gradle
    properties {
        property "sonar.projectName", "AndStatus"
        property "sonar.projectKey", "andstatus"
        property "sonar.projectVersion", project.ext.versionName

        property "sonar.sourceEncoding","UTF-8"
        // See http://docs.sonarqube.org/display/SONAR/Narrowing+the+Focus
        property "sonar.exclusions","build/**,libs/**,**/*.png,**/*.json,**/*.iml,**/*Secret.*"

        property "sonar.import_unknown_files", true

        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
        property "sonar.jacoco.reportPath", fileTree(dir: project.projectDir, includes: ['**/*.exec'])
        property "sonar.jacoco.itReportPath", fileTree(dir: project.projectDir, includes: ['**/*.ec'])
    }
}

dependencies {
    implementation "com.github.stephenc.jcip:jcip-annotations:$jcipAnnotationsVersion"
    // http://stephenc.github.io/jcip-annotations/dependency-info.html
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "ch.acra:acra:$acraVersion" // https://github.com/ACRA/acra/wiki/AdvancedUsage
    implementation "cz.msebera.android:httpclient:$httpclientVersion" // https://github.com/smarek/httpclient-android
    /* Don't use these dependencies for now, because AIDE doesn't support exclusion
                           of transitive dependencies.
                        compile 'org.apache.httpcomponents:httpmime:4.3.5'
                        Use the files from 'libs' folder instead:	*/
    implementation files('libs/httpmime-4.3.5.jar')
    implementation files('libs/signpost-httpclient-android-1.2.1.2.jar')
    implementation files('libs/signpost-core-1.2.1.2.jar')
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    implementation project(':draglistview')
    implementation "com.github.scribejava:scribejava-core:$scribejavaCoreVersion"
    implementation "junit:junit:$junitVersion"

    androidTestImplementation "com.android.support.test:runner:$supportTestVersion"
    androidTestImplementation "com.android.support.test:rules:$supportTestVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoCoreVersion"
    androidTestImplementation "com.android.support:support-annotations:$supportVersion"
}