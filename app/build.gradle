apply plugin: 'com.android.application'
apply plugin: "org.sonarqube"

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        versionCode 307
        versionName "53.03"

        applicationId "org.andstatus.app"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testApplicationId "org.andstatus.app.tests"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // To test arguments:
        // testInstrumentationRunnerArgument "executionMode", "travisTest"
        project.ext.archivesBaseName = "AndStatus-$versionName".toString()
        project.ext.versionName = "$versionName".toString()
    }

    buildTypes {
        release {
            minifyEnabled false
            lintOptions {
                warning 'MissingTranslation','InvalidPackage'
            }
        }
        debug {
            testCoverageEnabled = project.hasProperty('testCoverageEnabled') ? project.testCoverageEnabled as boolean : false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
}

sonarqube {
    // See https://docs.sonarqube.org/display/SCAN/Analyzing+with+SonarQube+Scanner+for+Gradle
    properties {
        property "sonar.projectName", "AndStatus"
        property "sonar.projectKey", "andstatus"
        property "sonar.projectVersion", project.ext.versionName

        property "sonar.sourceEncoding","UTF-8"
        // See http://docs.sonarqube.org/display/SONAR/Narrowing+the+Focus
        property "sonar.exclusions","build/**,libs/**,**/*.png,**/*.json,**/*.iml,**/*Secret.*"

        property "sonar.import_unknown_files", true

        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
        property "sonar.jacoco.reportPath", fileTree(dir: project.projectDir, includes: ['**/*.exec'])
        property "sonar.jacoco.itReportPath", fileTree(dir: project.projectDir, includes: ['**/*.ec'])
    }
}

configurations {
    all {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

dependencies {
    implementation "androidx.annotation:annotation:$annotationVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.documentfile:documentfile:$documentfileVersion" // https://developer.android.com/jetpack/androidx/releases/documentfile
    implementation "androidx.preference:preference:$preferenceVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefreshlayoutVersion" // https://developer.android.com/jetpack/androidx/releases/swiperefreshlayout
    implementation "ch.acra:acra-dialog:$acraVersion" // https://github.com/ACRA/acra/wiki/AdvancedUsage
    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "com.github.scribejava:scribejava-core:$scribejavaCoreVersion"
    implementation "com.github.stephenc.jcip:jcip-annotations:$jcipAnnotationsVersion" // http://stephenc.github.io/jcip-annotations/dependency-info.html
    implementation "com.google.android.material:material:$materialVersion"
    implementation "commons-codec:commons-codec:$commonsCodecVersion"
    implementation "cz.msebera.android:httpclient:$httpclientVersion" // https://github.com/smarek/httpclient-android
    implementation "io.vavr:vavr:$vavrVersion"
    implementation "junit:junit:$junitVersion"
    implementation "oauth.signpost:signpost-core:$signPostVersion"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    implementation "org.apache.httpcomponents:httpmime:$httpMimeVersion"
    implementation project(':draglistview')

    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"
}